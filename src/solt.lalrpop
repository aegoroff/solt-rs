use crate::{Expr};

grammar;

pub Solution = <Line> <Line*>;

Line = {
    <HeaderLine>,
    <ProjectBegin>,
    <ProjectEnd>,
    <ProjectSectionBegin>,
    <ProjectSectionContent>,
    <ProjectSectionEnd>,
};

HeaderLine = {
    <FirstLine>,
    <Version>,
    <Comment>,
};

FirstLine : Expr = <Identifier> <Identifier*> "," <Identifier*> <DigitOrDot> => Expr::FirstLine;

Version : Expr = <id:Identifier> "=" <r:Rvalue> => Expr::Version(Box::new(id),Box::new(r));

ProjectBegin : Expr = {
    "Project" <t:ProjectType> "=" <n:Str> "," <p:Str> "," <id:Str> => {
        Expr::ProjectBegin(Box::new(t),Box::new(n),Box::new(p),Box::new(id))
    },
};

ProjectSectionBegin : Expr = {
    "ProjectSection" "(" <name:Identifier> ")" "=" <stage:Identifier> => {
        Expr::ProjectSectionBegin(Box::new(name),Box::new(stage))
    },
};

ProjectEnd : Expr = {
    "EndProject" => Expr::ProjectEnd,
};

ProjectSectionEnd : Expr = {
    "EndProjectSection" => Expr::ProjectSectionEnd,
};

ProjectSectionContent : Expr = {
    <k:Guid> "=" <v:Guid> => {
        Expr::ProjectSectionContent(Box::new(k), Box::new(v))
    },
};

ProjectType : Expr = {
    "(" <g:Str> ")" => Expr::ProjectType(Box::new(g)),
};

Rvalue : Expr = {
    <Identifier>,
    <DigitOrDot>,
};

Comment : Expr =  COMMENT => Expr::Comment(<>.to_string());

DigitOrDot: Expr = r"[.0-9]+" => Expr::DigitOrDot(<>.to_string());

Guid: Expr = {
   "{" GUID "}" => Expr::Guid((<>).1.to_string()),
};

Identifier: Expr = r"[a-zA-Z]+" => Expr::Identifier(<>.to_string());

Str: Expr =  STR => Expr::Str(<>.to_string());

match {
    r"[[:alnum:]]{8}-[[:alnum:]]{4}-[[:alnum:]]{4}-[[:alnum:]]{4}-[[:alnum:]]{12}" => GUID,
    r"#[^\r\n]*" => COMMENT
} else {
    r#""[^"]+""# => STR,
    _
}