
use crate::ast::{Expr};
use crate::lex::{Tok};

grammar<'input>(input: &'input str);

pub Solution = <FirstLine> <Line*>;

Line = {
    <HeaderLine>,
    <ProjectBegin>,
    <ProjectEnd>,
    <SectionBegin>,
    <SectionContent>,
    <SectionEnd>,
};

HeaderLine = {
    <Comment>,
    <Version>,
};

FirstLine : Expr<'input> = <Identifier> <Identifier*> "comma" <Identifier*> <DigitOrDot> => Expr::FirstLine;

Version : Expr<'input> = <id:Identifier> "eq" <r:DigitOrDot> => Expr::Version(Box::new(id),Box::new(r));

ProjectBegin : Expr<'input> = {
    "open_element" <t:ProjectType> "eq" <n:Str> "comma" <p:Str> "comma" <id:Str> => {
        Expr::ProjectBegin(Box::new(t),Box::new(n),Box::new(p),Box::new(id))
    },
};

ProjectEnd : Expr<'input> = "close_element" => Expr::ProjectEnd;

SectionBegin : Expr<'input> = {
    "open_element" <name:Identifier> "eq" <stage:Identifier> => {
        Expr::ProjectSectionBegin(Box::new(name),Box::new(stage))
    },
};

SectionEnd : Expr<'input> = {
    "close_element" => Expr::SectionEnd,
};

SectionContent : Expr<'input> = {
    <k:SectionKey> "eq" <v:SectionValue> => {
        Expr::SectionContent(Box::new(k), Box::new(v))
    },
};

ProjectType : Expr<'input> = <g:Guid> => Expr::ProjectType(Box::new(g));

Comment : Expr<'input> = "comment" => Expr::Comment(<>);

DigitOrDot: Expr<'input> = "digit_and_dot" => Expr::DigitOrDot(<>);

Guid: Expr<'input> = "guid" => Expr::Guid(<>);

Identifier: Expr<'input> = "id" => Expr::Identifier(<>);

Str: Expr<'input> = "str" => Expr::Str(<>);

SectionKey: Expr<'input> = "section_key" => Expr::Str(<>);

SectionValue: Expr<'input> = "section_value" => Expr::Str(<>);

extern {
    type Location = usize;
    type Error = ();

    enum Tok<'input> {
        "comment" => Tok::Comment(<&'input str>),
        "str" => Tok::Str(<&'input str>),
        "section_key" => Tok::SectionKey(<&'input str>),
        "section_value" => Tok::SectionValue(<&'input str>),
        "guid" => Tok::Guid(<&'input str>),
        "id" => Tok::Id(<&'input str>),
        "digit_and_dot" => Tok::DigitsAndDots(<&'input str>),
        "comma" => Tok::Comma,
        "eq" => Tok::Eq,
        "open_element" => Tok::OpenElement(<&'input str>),
        "close_element" => Tok::CloseElement(<&'input str>),
    }
}